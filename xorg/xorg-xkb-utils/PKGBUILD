# Maintainer: Michael Walker <mike@barrucadu.co.uk>

pkgname=xorg-xkb-utils
pkgver=7.5
pkgrel=1
pkgdesc="X.org keyboard utilities"
arch=(i686)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxaw' 'libxkbfile')
makedepends=(inputproto)
source=(${url}/releases/individual/app/xkbutils-1.0.2.tar.bz2
        ${url}/releases/individual/app/xkbcomp-1.1.1.tar.bz2
        ${url}/releases/individual/app/xkbevd-1.1.0.tar.bz2
        ${url}/releases/individual/app/setxkbmap-1.1.0.tar.bz2
        ${url}/releases/individual/app/xkbprint-1.0.2.tar.bz2
        LICENSE.setxkbmap LICENSE.xkbevd LICENSE.xkbutils)
md5sums=('369a21641b7ca983d6641298e51d97f8'
         '38c387bacdc01038c8ac280588792bcf'
         '8d3c1485c2dd62ea7395a37377958a39'
         '2f902e0a89aaf2b19e06e7f26c6efb3a'
         '3d3eb10466442354d6b73b503b9829db'
         '7c20baa6a26c32a04b1b1d6cbaa626a7'
         '7b1fc39455375a25afe6f9594ff979ac'
         '7b1fc39455375a25afe6f9594ff979ac')

build() {
  cd "${srcdir}"
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      ./configure --prefix= --mandir=/share/man || return 1
      make || return 1
      popd
    fi
  done
}

package() {
  cd "${srcdir}"
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      make DESTDIR="${pkgdir}" install || return 1
      install -D -m644 COPYING "${pkgdir}"/share/licenses/${pkgname}/LICENSE.${i%-*}  || return 1
      popd
    fi
  done

  install -D -m644 "${srcdir}"/LICENSE.* "${pkgdir}/share/licenses/${pkgname}/"  || return 1
}
