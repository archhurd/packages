# Maintainer: Alexander Preisinger <alexander.preisinger@gmail.com>
# $Id: PKGBUILD 61431 2009-12-16 05:19:38Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=tk
pkgver=8.5.9
pkgrel=1

pkgdesc="A windowing toolkit for use with tcl"
arch=('i686')
url="http://tcl.sourceforge.net/"
license=('custom')
depends=("tcl=${pkgver}" 'libxss' 'libxft')
source=(http://downloads.sourceforge.net/sourceforge/tcl/tk${pkgver}-src.tar.gz)

build() {
  cd ${srcdir}/tk${pkgver}/unix  
  
  ./configure --prefix= --mandir=/share/man --enable-threads --disable-64bit
  
  make
}

package() {
  cd ${srcdir}/tk${pkgver}/unix  
  make INSTALL_ROOT=${pkgdir} install install-private-headers
  ln -sf wish8.5 ${pkgdir}/bin/wish
  
  # install private headers (FS#14388)
  cd ${srcdir}/tk${pkgver}
  for dir in compat generic generic/ttk unix; do
    install -dm755 ${pkgdir}/include/tk-private/$dir
    install -m644 -t ${pkgdir}/include/tk-private/$dir $dir/*.h
  done

  # install license
  install -Dm644 license.terms ${pkgdir}/share/licenses/${pkgname}/LICENSE

  # remove buildroot traces / fixes #3602
  sed -i \
  -e "s,^TK_BUILD_LIB_SPEC='-L.*/unix,TK_BUILD_LIB_SPEC='-L/lib," \
  -e "s,^TK_SRC_DIR='.*',TK_SRC_DIR='/include'," \
  -e "s,^TK_BUILD_STUB_LIB_SPEC='-L.*/unix,TK_BUILD_STUB_LIB_SPEC='-L/lib," \
  -e "s,^TK_BUILD_STUB_LIB_PATH='.*/unix,TK_BUILD_STUB_LIB_PATH='/lib," \
  ${pkgdir}/lib/tkConfig.sh
}
md5sums=('7cdeb9feb61593f58a0ae61f2497580e')
