# Maintainer: Matt Windsor <hayashi@archhurd.org>
# Contributor: SÃ©bastien Duquette <ekse.0x@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=libtasn1
pkgver=2.9
pkgrel=1
pkgdesc="The ASN.1 library used in GNUTLS"
arch=('i686')
license=('GPL3' 'LGPL')
url="http://www.gnu.org/software/libtasn1/"
depends=('glibc' 'texinfo')
options=('!libtool')
install=libtasn1.install
source=(ftp://ftp.gnu.org/gnu/libtasn1/${pkgname}-${pkgver}.tar.gz)

md5sums=('f4f4035b84550100ffeb8ad4b261dea9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix= \
      --with-packager=ArchHurd \
      --with-packager-bug-reports="http://bugs.archhurd.org/" \
      --with-packager-version=${pkgver}-${pkgrel}
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
